; TODO: add future support for languages 

;--------------------------------
; Build environment

  !define top_srcdir @top_srcdir@
  !define srcdir @srcdir@
  !define VERSION @PACKAGE_VERSION@
  !addplugindir @srcdir@

;--------------------------------
; Libtool executable target path

  !include libtoolexecutablesubdir.nsh
  !ifndef LT_EXEDIR
    !define LT_EXEDIR ""
  !endif

;--------------------------------
; General

  ;Name and file
  Name "MUNIL"
  OutFile "MUNIL-${VERSION}-SOFTWARE.exe"

  SetCompressor /SOLID LZMA

  ;Default installation folder
  InstallDir "$PROGRAMFILES\MUNIL"

  ;Get installation folder from registry if available
  InstallDirRegKey HKLM "Software\MUNIL" ""
 
  !include "MUI2.nsh"
  !include "WordFunc.nsh"
  !include "Library.nsh"
  !include "WinVer.nsh"
  !include "FileFunc.nsh"

  Var STARTMENU_FOLDER
  Var MUI_TEMP
 
;--------------------------------
;Configuration
 
  ;Bitmaps
  !define MUI_WELCOMEFINISHPAGE_BITMAP            ".\installer-pixmaps\wxmun-intro.bmp"
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_RIGHT
  !define MUI_HEADERIMAGE_BITMAP                  ".\installer-pixmaps\wxmun-header.bmp"

  ;Finish Page config
  !define MUI_FINISHPAGE_NOAUTOCLOSE
  !define MUI_FINISHPAGE_RUN                    "$INSTDIR\wxMUN.exe"
  !define MUI_FINISHPAGE_RUN_NOTCHECKED
  !define MUI_FINISHPAGE_LINK                   "Visit wxMUN homepage"
  !define MUI_FINISHPAGE_LINK_LOCATION          "http://wxmun.unitednetherlands.org"

  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "${top_srcdir}\COPYING"
  !insertmacro MUI_PAGE_COMPONENTS

  ; Install dir page
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER
  !insertmacro MUI_PAGE_INSTFILES
  !insertmacro MUI_PAGE_FINISH

  ; Uninstaller pages
  !insertmacro MUI_UNPAGE_WELCOME
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !insertmacro MUI_UNPAGE_FINISH

  ;Language
  !insertmacro MUI_LANGUAGE "English"
 
  ;General
  OutFile "wxmun-${VERSION}-setup-win32.exe"
 
;-------------------------------------------------------------------------
; Undef this if you dont have UPX upx.sourceforge.net
!define HAVE_UPX

!ifdef HAVE_UPX
  !packhdr tmpexe.tmp "UPX --best -q --compress-icons=0 tmpexe.tmp"
!endif

ShowInstDetails show
ShowUnInstDetails show

Var REG_ROOT_KEY

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY

  UserInfo::GetAccountType
  Pop $R0
  StrCmp $R0 "Admin" is_admin

	ReadRegStr $INSTDIR HKCU "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "PATH"
	IfErrors 0 +2
    StrCpy $INSTDIR "$PROFILE\wxMUN"
  SetShellVarContext current
  StrCpy $REG_ROOT_KEY "HKCU"
  Goto start

is_admin:
  SetShellVarContext all
  StrCpy $REG_ROOT_KEY "HKLM"

start:
FunctionEnd
 
;--------------------------------
;Installer macros

!macro WRITE_REG_DATA FUNCTION SUB_KEY ENTRY_NAME ENTRY_VALUE
  ${FUNCTION} SHCTX "${SUB_KEY}" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

!macro ADD_UNINSTALL_ENTRY_STR ENTRY_NAME ENTRY_VALUE
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

!macro ADD_UNINSTALL_ENTRY_DWORD ENTRY_NAME ENTRY_VALUE
  !insertmacro WRITE_REG_DATA WriteRegDWORD "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

;--------------------------------
;Installer Sections
 
Section "wxMUN" SecCore
SectionIn 1 RO

!ifdef NSIS_CONFIG_LOG
  LogSet on
!endif

  SetOutPath $INSTDIR

  File "..\src\${LT_EXEDIR}wxMUN.exe"
  File "${srcdir}\mingwm10.dll"
  File "${top_srcdir}\GPL.html"

  SetOutPath "$INSTDIR\share\wxmun\resources"
  File "${top_srcdir}/src\resources\countries.xml"
  File "${top_srcdir}/src\resources\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\16x16"
  File "${top_srcdir}/src\resources\16x16\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\32x32"
  File "${top_srcdir}/src\resources\32x32\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\48x48"
  File "${top_srcdir}/src\resources\48x48\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\flags"
  File "${top_srcdir}/src\resources\flags\*.png"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" DisplayName "wxMUN"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" UninstallString '"$INSTDIR\uninst.exe"'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" DisplayIcon $INSTDIR\gfx\icon.ico

SectionEnd
 
Section "Start menu icons" StartMenu
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application

  ;Create shortcuts
  SetOutPath "$INSTDIR"

  CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\wxMUN.lnk" "$INSTDIR\wxMUN.exe"
  CreateShortCut $SMPROGRAMS\$STARTMENU_FOLDER\License.lnk $INSTDIR\GPL.html

  ${If} ${AtLeastWin7}
    ; Setting AppID
    push "wxMUN.AppID"
    push "$SMPROGRAMS\$STARTMENU_FOLDER\wxMUN.lnk"
    nsis_appid::set_appid
  ${EndIf}

  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd
 
Section "Desktop icon" SecDesktop
  CreateShortcut "$DESKTOP\wxMUN.lnk" "$INSTDIR\wxMUN.exe"
SectionEnd
 
Section "Quick Launch icon" SecQuick
  StrCmp $QUICKLAUNCH $TEMP +2
    CreateShortcut "$QUICKLAUNCH\wxMUN.lnk" "$INSTDIR\wxMUN.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "" "$INSTDIR\wxMUN.exe"
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "PATH" "$INSTDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "" "$INSTDIR\wxMUN.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayName" "wxMUN"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "UninstallString" "$INSTDIR\uninst.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayIcon" "$INSTDIR\wxMUN.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayVersion" "${VERSION}"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "URLUpdateInfo" "http://wxmun.unitednetherlands.org"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "URLInfoAbout" "http://wxmun.unitednetherlands.org"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "InstallLocation" "$INSTDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "InstallSource" "$EXEDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "Publisher" "Geert-Jan Besjes"
  !insertmacro ADD_UNINSTALL_ENTRY_DWORD "NoModifiy" 1
  !insertmacro ADD_UNINSTALL_ENTRY_DWORD "NoRepair" 1
SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecCore} "Required core wxMUN files"
  !insertmacro MUI_DESCRIPTION_TEXT ${StartMenu} "Put links in the start menu"
  !insertmacro MUI_DESCRIPTION_TEXT ${SecQuick} "Add a link to wxMUN in your quick launch bar"
  !insertmacro MUI_DESCRIPTION_TEXT ${SecDesktop} "Place a link to wxMUN on the desktop"
!insertmacro MUI_FUNCTION_DESCRIPTION_END


;--------------------------------
;Uninstaller Section
 
!macro DELETE_REG_KEY REG_KEY
	StrCmp $REG_ROOT_KEY "HKLM" 0 +3
    DeleteRegKey HKLM "${REG_KEY}"
    Goto +2
 	DeleteRegKey HKCU "${REG_KEY}"
!macroend

Section "Uninstall"

  Delete "$INSTDIR\wxMUN.exe"
  Delete "$INSTDIR\mingwm10.dll"
  Delete "$INSTDIR\GPL.html"

  Delete "$DESKTOP\wxMUN.lnk"

  Delete "$INSTDIR\share\wxmun\resources\16x16\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\16x16"

  Delete "$INSTDIR\share\wxmun\resources\32x32\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\32x32"

  Delete "$INSTDIR\share\wxmun\resources\48x48\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\48x48"

  Delete "$INSTDIR\share\wxmun\resources\flags\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\flags"

  Delete "$INSTDIR\share\wxmun\resources\*.png"
  Delete "$INSTDIR\share\wxmun\resources\*.xml"

  RMDir "$INSTDIR\share\wxmun\resources"
  RMDir "$INSTDIR\share\wxmun"
  RMDir "$INSTDIR\share"

  Delete "$INSTDIR\po\*"
  RMDir "$INSTDIR\po"

  Delete "$INSTDIR\uninst.exe"
  RMDir "$INSTDIR"

  DeleteRegKey HKCU "Software\HM Software\wxMUN"
  DeleteRegKey /ifempty HKCU "Software\wxMUN"
  
  !insertmacro DELETE_REG_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN"
  !insertmacro DELETE_REG_KEY "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe"

  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP

  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\wxMUN.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\License.lnk"
 
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN"
 
  Delete "$DESKTOP\wxMUN.lnk"
  Delete "$QUICKLAUNCH\wxMUN.lnk"
 
  SetAutoClose false

SectionEnd
 
;eof
