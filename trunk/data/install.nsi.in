;--------------------------------
; Build environment

  !define top_srcdir @top_srcdir@
  !define srcdir @srcdir@
  !define VERSION @PACKAGE_VERSION@
  !addplugindir @srcdir@

;--------------------------------
;General

  ;Name and file
  Name "wxMUN ${VERSION}"
  OutFile "wxMUN-${VERSION}-setup.exe"

  SetCompressor /SOLID LZMA

  ;Default installation folder
  InstallDir "$PROGRAMFILES\wxMUN"

  ;Get installation folder from registry if available
  InstallDirRegKey HKLM "Software\wxMUN" ""

  RequestExecutionLevel user

;--------------------------------
; Libtool executable target path

  !include libtoolexecutablesubdir.nsh
  !ifndef LT_EXEDIR
    !define LT_EXEDIR ""
  !endif

;--------------------------------
;Include Modern UI and functions

  !include "MUI2.nsh"
  !include "WordFunc.nsh"
  !include Library.nsh
  !include "WinVer.nsh"
  !include "FileFunc.nsh"
  !include "@srcdir@\process_running.nsh"

;--------------------------------
;Variables

  Var MUI_TEMP
  Var STARTMENU_FOLDER

;--------------------------------
;Interface Settings

  !define MUI_ICON "${top_srcdir}/src/resources/wxMUN.ico"
;  !define MUI_UNICON "${srcdir}/uninstall.ico"
  !define MUI_UNICON "${NSISDIR}/Contrib/Graphics/Icons/modern-uninstall.ico"

  !define MUI_ABORTWARNING

  !define MUI_WELCOMEFINISHPAGE_BITMAP            ".\installer-pixmaps\wxmun-intro.bmp"
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_RIGHT
  !define MUI_HEADERIMAGE_BITMAP                  ".\installer-pixmaps\wxmun-header.bmp"


 ########## Pages ##########
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${top_srcdir}\COPYING"
!insertmacro MUI_PAGE_DIRECTORY

!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN "$INSTDIR\wxMUN.exe"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;-------------------------------------------------------------------------
; Undef this if you dont have UPX upx.sourceforge.net
; !define HAVE_UPX

!ifdef HAVE_UPX
  !packhdr tmpexe.tmp "UPX --best -q --compress-icons=0 tmpexe.tmp"
!endif

ShowInstDetails show
ShowUnInstDetails show

Var REG_ROOT_KEY

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY

  UserInfo::GetAccountType
  Pop $R0
  StrCmp $R0 "Admin" is_admin

	ReadRegStr $INSTDIR HKCU "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "PATH"
	IfErrors 0 +2
    StrCpy $INSTDIR "$PROFILE\wxMUN"
  SetShellVarContext current
  StrCpy $REG_ROOT_KEY "HKCU"
  Goto start

is_admin:
  SetShellVarContext all
  StrCpy $REG_ROOT_KEY "HKLM"

start:
FunctionEnd

;--------------------------------
;Languages

  !insertmacro MUI_LANGUAGE "English"

; installer

Section "wxMUN" SEC01
!ifdef NSIS_CONFIG_LOG
  LogSet on
!endif

  SetOutPath "$INSTDIR"

  File "..\src\${LT_EXEDIR}wxMUN.exe"
  File "${srcdir}\mingwm10.dll"
  File "${top_srcdir}\GPL.html"
;  File "${top_srcdir}\NEWS"
;  File "${top_srcdir}\AUTHORS"

  SetOutPath "$INSTDIR\share\wxmun\resources"
  File "${top_srcdir}/src\resources\countries.xml"
  File "${top_srcdir}/src\resources\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\16x16"
  File "${top_srcdir}/src\resources\16x16\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\32x32"
  File "${top_srcdir}/src\resources\32x32\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\48x48"
  File "${top_srcdir}/src\resources\48x48\*.png"

  SetOutPath "$INSTDIR\share\wxmun\resources\flags"
  File "${top_srcdir}/src\resources\flags\*.png"

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\uninstall.exe"

  ;Create shortcuts
  SetOutPath "$INSTDIR"
  CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\wxMUN.lnk" "$INSTDIR\wxMUN.exe"
SectionEnd

!macro WRITE_REG_DATA FUNCTION SUB_KEY ENTRY_NAME ENTRY_VALUE
  ${FUNCTION} SHCTX "${SUB_KEY}" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

!macro ADD_UNINSTALL_ENTRY_STR ENTRY_NAME ENTRY_VALUE
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

!macro ADD_UNINSTALL_ENTRY_DWORD ENTRY_NAME ENTRY_VALUE
  !insertmacro WRITE_REG_DATA WriteRegDWORD "Software\Microsoft\Windows\CurrentVersion\Uninstall\HM NIS Edit" "${ENTRY_NAME}" "${ENTRY_VALUE}"
!macroend

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "" "$INSTDIR\wxMUN.exe"
  !insertmacro WRITE_REG_DATA WriteRegStr "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe" "PATH" "$INSTDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "" "$INSTDIR\wxMUN.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayName" "wxMUN"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "UninstallString" "$INSTDIR\uninst.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayIcon" "$INSTDIR\wxMUN.exe"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "DisplayVersion" "${VERSION}"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "URLUpdateInfo" "http://wxmun.unitednetherlands.org"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "URLInfoAbout" "http://wxmun.unitednetherlands.org"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "InstallLocation" "$INSTDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "InstallSource" "$EXEDIR"
  !insertmacro ADD_UNINSTALL_ENTRY_STR "Publisher" "Geert-Jan Besjes"
  !insertmacro ADD_UNINSTALL_ENTRY_DWORD "NoModifiy" 1
  !insertmacro ADD_UNINSTALL_ENTRY_DWORD "NoRepair" 1
SectionEnd

############################################################################################
;                                 Uninstaller                                              ;
############################################################################################


Function un.onInit
  FindWindow $R0 "wxMUN"
  IsWindow $R0 0 +3
  MessageBox MB_ICONEXCLAMATION|MB_OK "Before uninstalling wxMUN you must close it."
  Abort
  
  UserInfo::GetAccountType
  Pop $R0
  StrCmp $R0 "Admin" is_admin

  SetShellVarContext current
  StrCpy $REG_ROOT_KEY "HKCU"
  Goto start

is_admin:
  SetShellVarContext all
  StrCpy $REG_ROOT_KEY "HKLM"

start:
FunctionEnd

!macro DELETE_REG_KEY REG_KEY
	StrCmp $REG_ROOT_KEY "HKLM" 0 +3
    DeleteRegKey HKLM "${REG_KEY}"
    Goto +2
 	DeleteRegKey HKCU "${REG_KEY}"
!macroend

Section Uninstall

  Delete "$INSTDIR\wxMUN.exe"
  Delete "$INSTDIR\mingwm10.dll"
  Delete "$INSTDIR\GPL.html"


  Delete "$DESKTOP\wxMUN.lnk"

  Delete "$INSTDIR\share\wxmun\resources\16x16\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\16x16"

  Delete "$INSTDIR\share\wxmun\resources\32x32\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\32x32"

  Delete "$INSTDIR\share\wxmun\resources\48x48\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\48x48"

  Delete "$INSTDIR\share\wxmun\resources\flags\*.png"
  RMDir "$INSTDIR\share\wxmun\resources\flags"

  Delete "$INSTDIR\share\wxmun\resources\*.png"
  Delete "$INSTDIR\share\wxmun\resources\*.xml"

  RMDir "$INSTDIR\resources\share\wxmun"
  RMDir "$INSTDIR\resources\share"
  RMDir "$INSTDIR\resources"

  Delete "$INSTDIR\po\*"
  RMDir "$INSTDIR\po"
  RMDir "$INSTDIR"

  DeleteRegKey HKCU "Software\HM Software\wxMUN"
  DeleteRegKey /ifempty HKCU "Software\wxMUN"
  
  !insertmacro DELETE_REG_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\wxMUN"
  !insertmacro DELETE_REG_KEY "Software\Microsoft\Windows\CurrentVersion\App Paths\wxMUN.exe"
  
  SetAutoClose false
SectionEnd
